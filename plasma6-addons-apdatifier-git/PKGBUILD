# Maintainer: wintersnowgod <git.xerox732@passinbox.com>
_pkgname=apdatifier
pkgname=plasma6-applets-apdatifier-git
pkgver=r406.b35dc11
pkgrel=1
pkgdesc="KDE Plasma widget that tracks updates for ArchLinux, Plasma Widgets, and Flatpak, providing notifications for new ones."
arch=('any')
url="https://github.com/exequtic/apdatifier"
provides=('plasma6-applets-apdatifier')
conflicts=('plasma6-applets-apdatifier-bin' 'plasma6-applets-apdatifier')
license=('MIT')
depends=('kitemmodels' 'ksvg' 'qt6-declarative' 'libplasma' 'kirigami' 'knotifications' 'plasma5support' 'kcmutils' 'kdeclarative' 'kiconthemes' 'qt6-5compat' 'bash')
makedepends=('kpackage' 'git')
optdepends=('yay: For AUR updates support'
  'paru: For AUR updates support'
  'pikaur: For AUR updates support'
  'flatpak: For Flatpak updates support'
  'fwupd: For Firmware updates support'
  'jq: For KDE Widgets updates and News'
  'tmux: For upgrade and management in a terminal multiplexer'
  'fzf: For interactive package selection in management')
source=("${_pkgname}::git+${url}.git")
sha256sums=('SKIP')

pkgver() {
  cd "${_pkgname}"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

build() {
  local base="${srcdir}/${_pkgname}/package"
  local dir="${base}/translate"

  pushd -- "$dir" >/dev/null || return 1
  ./build >/dev/null
  popd >/dev/null || true

  rm -rf -- "$dir"
}

package() {
  _path="$pkgdir/usr/share/plasma/plasmoids/com.github.exequtic.apdatifier"
  mkdir -p "$_path"
  cp -r "$srcdir/${_pkgname}/package"/* "$_path"
}
